// Conway's Game of Life
// Touch to spark new life.

#ifdef GL_FRAGMENT_PRECISION_HIGH
precision highp float;
#else
precision mediump float;
#endif

uniform vec2 resolution;
uniform int pointerCount;
uniform vec3 pointers[10];
uniform sampler2D backbuffer;

int get( int x, int y )
{
	return int( texture2D(
		backbuffer,
		(gl_FragCoord.xy+vec2( x, y ))/resolution ).r );
}

vec3 evaluate( int sum )
{
	if( sum == 3 )
		return vec3( 1.0 );

	if( sum == 2 )
		return vec3( float( get( 0, 0 ) ) );

	return vec3( 0.0 );
}

void main()
{
	int sum =
		get( -1, -1 )+
		get( -1,  0 )+
		get( -1,  1 )+
		get(  0, -1 )+
		get(  0,  1 )+
		get(  1, -1 )+
		get(  1,  0 )+
		get(  1,  1 );

	for( int n = 0; n < pointerCount; ++n )
	{
		if( distance(
			pointers[n].xy,
			gl_FragCoord.xy ) < 10.0 )
		{
			sum = 3;
			break;
		}
	}

	gl_FragColor = vec4( evaluate( sum ), 1.0 );
}
